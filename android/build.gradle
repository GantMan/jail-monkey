def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}
def isNewArchitectureEnabled() {
   return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  if (major == 7 && minor >= 3) {
    return true
  }

  return major >= 8
}

apply plugin: 'com.android.library'

if (isNewArchitectureEnabled()) {
    apply plugin: 'com.facebook.react'
}

android {
    if (supportsNamespace()) {
        namespace 'com.gantix.JailMonkey'
        sourceSets {
          main {
            manifest.srcFile "src/main/AndroidManifestNew.xml"
          }
        }
    }
    compileSdkVersion safeExtGet('compileSdkVersion', 31)
    buildToolsVersion safeExtGet('buildToolsVersion', "31.0.0")

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 31)
        versionCode 1
        versionName "1.0"
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
    }

    sourceSets {
       main {
           if (isNewArchitectureEnabled()) {
               java.srcDirs += [
                    'src/newarch/java',
                    "${project.buildDir}/generated/source/codegen/java"
                ]
           } else {
               java.srcDirs += ['src/oldarch/java']
           }
       }
    }
    lintOptions {
       warning 'InvalidPackage'
    }
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation 'com.scottyab:rootbeer-lib:0.1.1'
}

if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../specs/")
        libraryName = "JailMonkey"
        codegenJavaPackageName = "com.gantix.JailMonkey"
    }
}
